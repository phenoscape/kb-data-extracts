#For Characiformes:  species, family, order, collection code, collection number, phenotype (EQ)
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ps: <http://purl.org/phenoscape/vocab.owl#>
PREFIX has_character: <http://purl.obolibrary.org/obo/CDAO_0000142>
PREFIX has_state: <http://purl.obolibrary.org/obo/CDAO_0000184>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX ow: <http://purl.org/phenoscape/owlet/syntax#>
PREFIX PelvicFin: <http://purl.obolibrary.org/obo/UBERON_0000152>
PREFIX PectoralFin: <http://purl.obolibrary.org/obo/UBERON_0000151>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX belongs_to_tu: <http://purl.obolibrary.org/obo/CDAO_0000191>
PREFIX has_external_reference: <http://purl.obolibrary.org/obo/CDAO_0000164>
PREFIX Characiformes: <http://purl.obolibrary.org/obo/VTO_0037519>
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX Family: <http://purl.obolibrary.org/obo/TAXRANK_0000004>

SELECT DISTINCT (STR(?taxon) AS ?taxon_id) (STR(?taxon_label) AS ?taxon_name) (STR(?family_label) AS ?family_name) (STR(?entity) AS ?entity_id) (STR(?entity_label) AS ?entity_name) (STR(?quality) AS ?quality_id) (STR(?quality_label) AS ?quality_name) (STR(?related_entity) AS ?related_entity_id) (STR(?related_entity_label) AS ?related_entity_name) (STR(?collection) AS ?collection_code) (STR(?catalog_number) AS ?cat_number)
FROM <http://kb.phenoscape.org/>
FROM <http://purl.obolibrary.org/obo/tto.owl>
WHERE
{	
?cell has_state: ?state .
?cell belongs_to_tu: ?otu .
?otu has_external_reference: ?taxon .
?taxon rdfs:subClassOf* Characiformes: .
?taxon rdfs:label ?taxon_label .
?taxon rdfs:subClassOf* ?vto_family .
?vto_family rdfs:label ?family_label .
?vto_family oboInOwl:hasDbXref ?xref .
?tto_family oboInOwl:hasDbXref ?xref .
?tto_family <http://purl.obolibrary.org/obo/tto#has_rank> Family: .
?state ps:denotes_exhibiting/rdf:type ?phenotype .
?phenotype ps:entity_term ?entity .
?entity rdfs:label ?entity_label .
?phenotype ps:quality_term ?quality .
?quality rdfs:label ?quality_label .
OPTIONAL {
	?phenotype ps:related_entity_term ?related_entity .
	?related_entity rdfs:label ?related_entity_label .
}
?otu dwc:individualID ?specimen .
?specimen dwc:collectionID/rdfs:label ?collection .
?specimen dwc:catalogNumber ?catalog_number .
}
